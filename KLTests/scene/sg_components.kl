require Math;
require GGscene;
require Fbx;
require Geometry;

object TestSG : CCTestCase
{
};

TestSG.testEntityBasics!()
{
  String name = 'myEntity';
  Entity e(name);
  this.Assert(e.getName() == name);
  this.Assert(e.getUID() == 0);
  Entity e2(name);
  this.Assert(e2.getUID() == 1);
}

TestSG.testUpdateComponentBasics!()
{
  Entity e('myEntity');
  UpdateComponent uc = UpdateComponent();

  e.addUpdateComponent(uc);
  this.Assert(uc.getOwnerEntity().getName() == e.getName());
  this.AssertEquals(e.getUpdateComponentCount(), 1);

  e.removeUpdateComponent(uc);
  this.AssertEquals(e.getUpdateComponentCount(), 0);
}

object CustomUpdateComponent : UpdateComponent {
  Float32 myFloat;
  Integer onStartCalls, onUpdateCalls, onDestroyCalls;
};
CustomUpdateComponent(String name, Float32 f) {
  this.createComponent(name);
  this.onStartCalls = this.onUpdateCalls = this.onDestroyCalls = 0;
}
CustomUpdateComponent.onStart!() 
{
	this.onStartCalls++;
}
CustomUpdateComponent.onUpdate!() 
{
	this.onUpdateCalls++;
}
CustomUpdateComponent.onDestroy!() 
{
	this.onDestroyCalls++;
}

TestSG.testUpdateComponentCustom!()
{
  Entity e('myEntity');
  CustomUpdateComponent uc = CustomUpdateComponent('CustomUpdateComponent', 0.123);

  Boolean ret = e.addUpdateComponent(uc);
  this.AssertEquals(ret, true);
  this.Assert(uc.getOwnerEntity().getName() == e.getName());
  this.AssertEquals(e.hasUpdateComponent(uc), true);
  this.AssertEquals(e.hasUpdateComponent('CustomUpdateComponent'), true);
  this.AssertEquals(e.hasUpdateComponent('UnexistingCustomComponent'), false);

  // make sure we can't add the same component twice !
  ret = e.addUpdateComponent(uc);
  this.AssertEquals(ret, false);
  this.AssertEquals(e.getUpdateComponentCount(), 1);

  CustomUpdateComponent anotherUC = CustomUpdateComponent('CustomUpdateComponent', 0.456);
  ret = e.addUpdateComponent(anotherUC);
  this.AssertEquals(ret, false);
  this.AssertEquals(e.getUpdateComponentCount(), 1);

  CustomUpdateComponent cc = e.getUpdateComponent('CustomUpdateComponent');
  this.AssertEquals(cc, uc);
}

TestSG.testGetUpdateComponents!()
{
  Entity e('myEntity');
  CustomUpdateComponent uc1 = CustomUpdateComponent('uc1', 0.123);
  CustomUpdateComponent uc2 = CustomUpdateComponent('uc2', 0.456);
  CustomUpdateComponent uc3 = CustomUpdateComponent('uc3', 0.789);

	e.addUpdateComponent(uc1);
	e.addUpdateComponent(uc2);
  e.addUpdateComponent(uc3);

  UpdateComponentArray updateComponents = e.getUpdateComponents();
  this.AssertEquals(updateComponents.size(), 3);

  e.update();
  e.update();
  e.update();
  e.update();

  this.AssertEquals(uc1.onStartCalls, 1);
  this.AssertEquals(uc2.onStartCalls, 1);
  this.AssertEquals(uc3.onStartCalls, 1);
  this.AssertEquals(uc1.onUpdateCalls, 3);
  this.AssertEquals(uc2.onUpdateCalls, 3);
  this.AssertEquals(uc3.onUpdateCalls, 3);
}


object SuicideUpdateComponent : UpdateComponent {
	Boolean called;
};
SuicideUpdateComponent(String name) {
  this.createComponent(name);
  this.called = false;
}
SuicideUpdateComponent.onStart!() 
{
	// do something then remove self
	this.called = true;
	this.getOwnerEntity().safeRemoveUpdateComponent(UpdateComponent(this));
}


TestSG.testSuicideUpdateComponent!()
{
    Entity e('myEntity');
    SuicideUpdateComponent suc = SuicideUpdateComponent('suc');
    CustomUpdateComponent cuc = CustomUpdateComponent('cuc', 0.123f);

    e.addUpdateComponent(suc);
    e.addUpdateComponent(cuc);

    this.AssertEquals(e.getUpdateComponentCount(), 2);
    e.update();
    this.AssertEquals(e.getUpdateComponentCount(), 1);

    this.AssertEquals(suc.called, true);
    this.AssertEquals(cuc.onStartCalls, 1);
    this.AssertEquals(cuc.onUpdateCalls, 0);
}

TestSG.testTransformComponentBasics!()
{
  String eName('myEntity');
  Entity e(eName);
  TransformComponent tc = e.getTransformComponent();
  this.Assert(tc.getOwnerEntity().getName() == e.getName());
  this.Assert(tc.getChildCount() == 0);
  this.Assert(e.getLocalTransform() == Xfo());
  this.AssertEquals(e.getPath(), "/" + eName);

  String ecName('myEntityChild');
  Entity ec('myEntityChild');
  e.addChild(ec);
  this.Assert(ec.getParent() == e);
  this.Assert(tc.getChildCount() == 1);
  this.Assert(ec.getLocalTransform() == Xfo());
  this.AssertEquals(ec.getPath(), String("/" + eName + "/" + ecName));
}
