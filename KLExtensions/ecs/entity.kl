require Math;
require Singletons;

object CCUpdateComponent;
object CCTransformComponent;
object CCProperties;

alias CCUpdateComponent UpdateComponentArray[];

object CCEntity
{
  private Index uid;
  private String name;

  private CCTransformComponent transformComponent;
  private UpdateComponentArray updateComponents;
  private UpdateComponentArray updateComponentsToRemove;

  private CCProperties properties;
};

CCEntity.appendDesc(io String s)
{
  s += this.toString();
} 

String CCEntity.toString()
{
  if (this == null) return 'null CCEntity';
  return String(this.name 
               + ' paths=' + this.getPaths() 
               + ' properties=' + this.properties.toString()
               // + ' transformComponent=' + this.transformComponent
               // + ' updateComponents=' + this.updateComponents 
               );
} 

// constructors
CCEntity.createEntity!(String n, io CCTransformComponent tc)
{
  CCEntityFactory ef = getEntityFactory();

  this.uid = ef.getUID();
  this.name = n;
  this.properties = CCProperties();
  this.setTransformComponent(tc);
}

CCEntity(String n)
{
  this.createEntity(n, CCTransformComponent());
}

CCEntity(String n, io CCUpdateComponent uc)
{
  this.createEntity(n, CCTransformComponent());
  this.addUpdateComponent(uc);
}

CCEntity(String n, Xfo xfo)
{
  this.createEntity(n, CCTransformComponent());
  this.setLocalTransform(xfo);
}

~CCEntity()
{
  report('byebye'); // doesnt work ???
}

Index CCEntity.getUID()
{ 
  return this.uid; 
}

String CCEntity.getName()
{ 
  return this.name; 
}

CCTransformComponent CCEntity.getTransformComponent()
{ 
  return this.transformComponent; 
}

CCProperties CCEntity.getProperties()
{ 
  return this.properties; 
}

String CCEntity.getPath()
{ 
  return this.transformComponent.getPath(); 
}

String[] CCEntity.getPaths()
{ 
  return this.transformComponent.getPaths(); 
}

Xfo CCEntity.getLocalTransform!()
{ 
  return this.transformComponent.getLocalTransform(); 
}

Xfo CCEntity.getGlobalTransform!()
{ 
  return this.transformComponent.getGlobalTransform(); 
}

XfoArray CCEntity.getGlobalTransforms!()
{ 
  return this.transformComponent.getGlobalTransforms(); 
}

Size CCEntity.getChildCount!()
{ 
  return this.transformComponent.getChildCount(); 
}

CCEntity CCEntity.getChild!(Index i)
{ 
  return this.transformComponent.getChild(i); 
}

Boolean CCEntity.hasParent()
{ 
  return this.transformComponent.hasParent(); 
}

CCEntity CCEntity.getParent()
{ 
  return this.transformComponent.getParent(0); 
}

Ref<CCEntity>[] CCEntity.getParents()
{ 
  return this.transformComponent.getParents(); 
}

Boolean CCEntity.isInstanced()
{
  Boolean isInstanced = this.getParents().size() > 1;
  if (isInstanced) return true;
  if (this.hasParent() == false) return false;
  return this.getParent().isInstanced();
}

Size CCEntity.getUpdateComponentCount()
{ 
  return this.updateComponents.size(); 
}

CCEntity.setLocalTransform!(Xfo tr)
{
  this.transformComponent.setLocalTransform(tr);
}

CCEntity.setTransformComponent!(io CCTransformComponent tc)
{ 
  this.transformComponent = tc; 
  tc.setOwnerEntity(this);
}

Boolean CCEntity.addUpdateComponent!(io CCUpdateComponent uc)
{
  if (this.hasUpdateComponent(uc)) return false;
  this.updateComponents.push(uc);
  uc.setOwnerEntity(this);
  return true;
}

Boolean CCEntity.removeUpdateComponent!(io CCUpdateComponent uc)
{
  if (this.hasUpdateComponent(uc) == false) return false;

  UpdateComponentArray newComponents;
  for(Size i=0; i<this.updateComponents.size(); ++i) 
  {
    if (this.updateComponents[i] != uc)
      newComponents.push(uc);
  }
  this.updateComponents = newComponents;

  return true;
}

Boolean CCEntity.safeRemoveUpdateComponent!(io CCUpdateComponent uc)
{
  if (this.hasUpdateComponent(uc) == false) return false;
  this.updateComponentsToRemove.push(uc);
  return true;  
}

Boolean CCEntity.hasUpdateComponent(CCUpdateComponent uc)
{
  return this.hasUpdateComponent(uc.getName());
}

Boolean CCEntity.hasUpdateComponent(String ucName)
{
  for(Size i=0; i<this.updateComponents.size(); ++i) 
  {
    if (this.updateComponents[i].getName() == ucName)
      return true;
  }
  return false;
}

CCUpdateComponent CCEntity.getUpdateComponent(String ucName)
{
  for(Size i=0; i<this.updateComponents.size(); ++i) 
  {
    if (this.updateComponents[i].getName() == ucName)
      return this.updateComponents[i];
  }
  return null;
}

UpdateComponentArray CCEntity.getUpdateComponents()
{
  return this.updateComponents;
}

Boolean == (CCEntity e1, CCEntity e2)
{
  return e1 != null && e2 != null && e1.getUID() == e2.getUID();
}

Boolean != (CCEntity e1, CCEntity e2)
{
  return !(e1 == e2);
}

CCEntity.addChild!(io CCEntity e)
{
  this.addChild(e, false);
}

CCEntity.addChild!(io CCEntity e, Boolean reparentChild)
{
  if (reparentChild) {
    Xfo invTr = this.getGlobalTransform().inverse();
    Xfo newChildTr = invTr * e.getGlobalTransform();
    e.setLocalTransform(newChildTr);

    // if we do reparent we have to clear our parents array
    e.getTransformComponent().clearParents();  
    // if (e.hasParent()) e.getTransformComponent().removeParent(e.getParent());  
  }

  this.transformComponent.addChild(e);
  e.getTransformComponent().addParent(this);
}

CCEntity.destroy()
{
  // foreach(CCEntity p in this.getParents()) {
  //   p.removeChild(this);
  // }
}

CCEntity.update!()
{
  for(Index i=0; i<this.updateComponents.size(); ++i)
  {
    CCUpdateComponent uc = this.updateComponents[i];

    if (uc.started == false)
    {
      uc.onStart();
      uc.started = true;
    } 
    else
    {
      uc.onUpdate();
    }
  }

  for(Index i=0; i<this.getChildCount(); ++i)
  {
    this.getChild(i).update();
  }

  if (this.updateComponentsToRemove.size() > 0)
  {
    for(Index i=0; i<this.updateComponentsToRemove.size(); ++i)
    {
      CCUpdateComponent uc = this.updateComponents[i];
      this.removeUpdateComponent(uc);
    }
    this.updateComponentsToRemove.resize(0);
  }  
}
